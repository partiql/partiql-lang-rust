use crate::parse::lexer;
use crate::error::ParseError;

use lalrpop_util as lpop;

use std::str::FromStr;

use partiql_ast::ast;

use partiql_source_map::location::{ByteOffset, BytePosition, Location, ToLocated};

use crate::parse::parse_util::{strip_query};
use crate::parse::parser_state::{ParserState, IdGenerator};

grammar<'input, 'state, Id>(input: &'input str, state: &'state mut ParserState<'input, Id>) where Id: IdGenerator;


// ------------------------------------------------------------------------------ //
//                                                                                //
//                            UNION/INTERSECT/EXCEPT                              //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Note: the Set operation rules are formulated in a precise way to assure 'natural'
//    interpretation of set operations in an LR grammar while attempting
//    to stay in accordance with SQL interpretations.
//
// In particular:
//    - in order to assure operator precedence, the lowest precedence operators
//      (i.e., `UNION` & `EXCEPT`) are at the top of the recursion tree, and the highest
//      precedence are at the bottom (i.e., `INTERSECT`)
//    - all set operations are left-associative and are thus expressed as left-self-recursive rules

pub(crate) Query: Box<ast::Expr> = {
    <lo:@L>
    <set:QuerySetPrec02>
    <order_by:OrderByClause?>
    <limit:LimitClause?>
    <offset:OffsetByClause?>
    <hi:@R> => {
        Box::new(ast::Expr::Query( state.node(ast::Query { set, order_by, limit, offset, }, lo..hi) ) )
    }
}

QuerySetPrec02: ast::AstNode<ast::QuerySet> = {
    <lo:@L> <lhs:QuerySetPrec02> <setop:SetOpUnionExcept> <setq:SetQuantifier> <rhs:QuerySetPrec01> <hi:@R> => {
        let set_expr = state.node(ast::SetExpr {
             setop,
             setq,
             lhs: Box::new(lhs),
             rhs: Box::new(rhs)
        }, lo..hi);
        state.node(ast::QuerySet::SetOp(Box::new( set_expr )), lo..hi)
	},
    <QuerySetPrec01>
}

QuerySetPrec01: ast::AstNode<ast::QuerySet> = {
    <lo:@L> <lhs:QuerySetPrec01> <setop:SetOpIntersect> <setq:SetQuantifier> <rhs:SingleQuery> <hi:@R> => {
        let set_expr = state.node(ast::SetExpr {
            setop,
            setq,
            lhs: Box::new(lhs),
            rhs: Box::new(rhs)
        }, lo..hi);
        state.node(ast::QuerySet::SetOp(Box::new( set_expr )), lo..hi)
	},
    <SingleQuery>
}

#[inline]
SetOpUnionExcept: ast::SetOperator = {
    "UNION" => ast::SetOperator::Union,
    "EXCEPT" => ast::SetOperator::Except,
}

#[inline]
SetOpIntersect: ast::SetOperator = {
    "INTERSECT" => ast::SetOperator::Intersect
}

#[inline]
SetQuantifier: ast::SetQuantifier = {
    "DISTINCT" => ast::SetQuantifier::Distinct,
    "ALL"? => ast::SetQuantifier::All,
}



// ------------------------------------------------------------------------------ //
//                                                                                //
//                            ExprQuery or SFW Query                              //
//                                                                                //
// ------------------------------------------------------------------------------ //
SingleQuery: ast::AstNode<ast::QuerySet> = {
    <lo:@L> <expr:ExprQuery> <hi:@R> => {
        match *expr {
           ast::Expr::Query(ast::AstNode{ node: ast::Query{set, order_by:None, limit:None, offset:None} , .. }) => set,
           _ => state.node(ast::QuerySet::Expr( expr ), lo..hi),
        }
    },
    <lo:@L> <sfw:SfwQuery> <hi:@R> => state.node(ast::QuerySet::Select( Box::new(sfw)), lo..hi),
    <lo:@L> <values:Values> <hi:@R> => values,
}

Values: ast::AstNode<ast::QuerySet> = {
    <lo:@L> "VALUES" <rows:CommaSepPlus<ValueRow>> <hi:@R> => state.node(ast::QuerySet::Values( rows ), lo..hi)
}

#[inline]
ValueRow: Box<ast::Expr> = {
    "(" <e:ExprQuery> ")" => Box::new(*e),
    <array:ExprTermCollection> => Box::new(array)
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                   SFW Query                                    //
//                                                                                //
// ------------------------------------------------------------------------------ //
SfwQuery: ast::AstNode<ast::Select> = {
    <SfwClauses>,
    <FwsClauses>
}

// SQL-style where `Select` precedes `From`
SfwClauses: ast::AstNode<ast::Select> = {
    <lo:@L>
    <with:WithClause?>
    <project:SelectClause>
    <from:FromClause?>
    <where_clause:WhereClause?>
    <group_by:GroupClause?>
    <having:HavingClause?>
    <hi:@R> => {
        state.node(ast::Select {
            project,
            from,
            from_let: None,
            where_clause,
            group_by,
            having,
        }, lo..hi)
    }
}

// PartiQL-style where `Select` is last
FwsClauses: ast::AstNode<ast::Select> = {
    <lo:@L>
    <with:WithClause?>
    <from:FromClause>
    <where_clause:WhereClause?>
    <group_by:GroupClause?>
    <having:HavingClause?>
    <project:SelectClause>
    <hi:@R> => {
        state.node(ast::Select {
            project,
            from: Some(from),
            from_let: None,
            where_clause,
            group_by,
            having,
        }, lo..hi)
    }
}

// ------------------------------------------------------------------------------ //
//                                     WITH                                       //
// ------------------------------------------------------------------------------ //
// TODO
WithClause: () = {}

// ------------------------------------------------------------------------------ //
//                                    SELECT                                      //
// ------------------------------------------------------------------------------ //
SelectClause: ast::AstNode<ast::Projection> = {
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> "*" <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectStar,
        setq: Some(strategy)
    }, lo..hi),
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> <project_items:CommaSepPlus<Projection>> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectList(project_items),
        setq: Some(strategy),
    }, lo..hi),
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> "VALUE" <value:ExprQuery> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectValue(value),
        setq: Some(strategy),
    }, lo..hi),
    <lo:@L> "PIVOT" <value:ExprQuery> "AT" <key:ExprQuery> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectPivot { key, value },
        setq: None
    }, lo..hi),
}

#[inline]
SetQuantifierStrategy: ast::SetQuantifier = {
    "ALL" => ast::SetQuantifier::All,
    <distinct: "DISTINCT"?> => {
        match distinct {
            Some(_) => ast::SetQuantifier::Distinct,
            None => ast::SetQuantifier::All,
        }
    }
}

#[inline]
Projection: ast::AstNode<ast::ProjectItem> = {
    <lo:@L> <expr:ExprQuery> <hi:@R>
        => state.node(ast::ProjectItem::ProjectExpr( ast::ProjectExpr{ expr, as_alias: None } ), lo..hi),
    <lo:@L> <expr:ExprQuery> "AS"? <as_alias:SymbolPrimitive> <hi:@R> => {
        state.node(ast::ProjectItem::ProjectExpr( ast::ProjectExpr{ expr, as_alias: Some(as_alias) } ), lo..hi)
    },
}

// ------------------------------------------------------------------------------ //
//                                     FROM                                       //
// ------------------------------------------------------------------------------ //
FromClause: ast::AstNode<ast::FromClause> = {
    <lo:@L> "FROM" <mut froms:(<TableReference> "," "LATERAL"?)*> <last:TableReference> <hi:@R> => {
        let total: Location<BytePosition> = Location::from(lo.into()..hi.into());

        // use `reduce` to process the comma-seperated `TableReference`s
        //   as left-associative `CROSS JOIN`s
        froms.push(last);
        froms.into_iter()
            .reduce(|lfrom, rfrom| {
	            let start = state.locations.get(&lfrom.id).unwrap_or(&total).start.0.clone();
                let end = state.locations.get(&rfrom.id).unwrap_or(&total).end.0.clone();
                let range = start..end;
                let join = state.node(ast::Join {
                    kind: ast::JoinKind::Cross,
                    left: Box::new(lfrom),
                    right: Box::new(rfrom),
                    predicate: None
                }, range.clone());
                state.node(ast::FromClause::Join( join ), range)
            })
            .unwrap() // safe, because we know there's at least 1 input
    }
}

TableReference: ast::AstNode<ast::FromClause> = {
    <TableNonJoin>,
    <TableJoined>,
}

TableNonJoin: ast::AstNode<ast::FromClause> = {
    <lo:@L> <t:TableBaseReference> <hi:@R> => state.node(ast::FromClause::FromLet( t ), lo..hi),
    <lo:@L> <t:TableUnpivot> <hi:@R> => state.node(ast::FromClause::FromLet( t ), lo..hi),
}

#[inline]
TableBaseReference: ast::AstNode<ast::FromLet> = {
    <lo:@L> <e:ExprQuery> <as_alias:SymbolPrimitive> <hi:@R> => {
        state.node(ast::FromLet {
            expr: e,
            kind: ast::FromLetKind::Scan,
            as_alias: Some(as_alias),
            at_alias: None,
            by_alias: None
        }, lo..hi)
    },
    <lo:@L> <e:GraphMatchExprNoParens> <as_alias:AsIdent?> <at_alias:AtIdent?> <by_alias:ByIdent?> <hi:@R> => {
        state.node(ast::FromLet {
            expr: Box::new(e),
            kind: ast::FromLetKind::Scan,
            as_alias,
            at_alias,
            by_alias
        }, lo..hi)
    },
    <lo:@L> <e:ExprQuery> <as_alias:AsIdent?> <at_alias:AtIdent?> <by_alias:ByIdent?> <hi:@R> => {
        state.node(ast::FromLet {
            expr: e,
            kind: ast::FromLetKind::Scan,
            as_alias,
            at_alias,
            by_alias
        }, lo..hi)
    }
}

#[inline]
TableUnpivot: ast::AstNode<ast::FromLet> = {
    <lo:@L> "UNPIVOT" <e:ExprQuery> <as_ident:AsIdent?> <at_ident:AtIdent?> <hi:@R> => {
        state.node(ast::FromLet {
            expr: e,
            kind: ast::FromLetKind::Unpivot,
            as_alias: as_ident,
            at_alias: at_ident,
            by_alias: None,
        }, lo..hi)
    }
}

TableJoined: ast::AstNode<ast::FromClause> = {
    <TableCrossJoin>,
    <TableQualifiedJoin>,
    "(" <TableJoined> ")",
}

#[inline]
TableCrossJoin: ast::AstNode<ast::FromClause> = {
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <j:JoinType?> "CROSS" "JOIN" <rtable:JoinRhs> <hi:@R> => {
        let kind = match j {
            Some(j) => j,
            None => ast::JoinKind::Cross
        };
        let join = state.node(ast::Join {
            kind,
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: None
        }, lo..hi);
        state.node(ast::FromClause::Join( join ), lo..hi)
    }
}

#[inline]
TableQualifiedJoin: ast::AstNode<ast::FromClause> = {
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <j:JoinType> "JOIN" "LATERAL"? <rtable:JoinRhs> <on:JoinSpec> <hi:@R> => {
        let join = state.node(ast::Join {
            kind: j,
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: Some(on),
        }, lo..hi);
        state.node(ast::FromClause::Join( join ), lo..hi)
    },
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <spec:JoinSpecNatural> <j:JoinType> "JOIN" "LATERAL"? <rtable:JoinRhs> <hi:@R> => {
        let join = state.node(ast::Join {
            kind: j,
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: Some(spec)
        }, lo..hi);
        state.node(ast::FromClause::Join( join ), lo..hi)
    },
}
#[inline]
JoinRhs: ast::AstNode<ast::FromClause> = {
    <TableNonJoin>,
    "(" <TableJoined> ")"
}
#[inline]
JoinSpecNatural: ast::AstNode<ast::JoinSpec> = {
    <lo:@L> "NATURAL" <hi:@R> => state.node(ast::JoinSpec::Natural, lo..hi)
}
#[inline]
JoinType: ast::JoinKind = {
    "INNER" => ast::JoinKind::Inner,
    "LEFT" "OUTER"? => ast::JoinKind::Left,
    "RIGHT" "OUTER"? => ast::JoinKind::Right,
    "FULL" "OUTER"? => ast::JoinKind::Full,
    "OUTER" => ast::JoinKind::Full,
}
#[inline]
JoinSpec: ast::AstNode<ast::JoinSpec> = {
    <lo:@L> "ON" <e:ExprQuery> <hi:@R> => state.node(ast::JoinSpec::On(e), lo..hi),
    <lo:@L> "USING" "(" <paths:CommaSepPlus<PathExpr>> ")" <hi:@R> => state.node(ast::JoinSpec::Using( paths ), lo..hi),
}

// ------------------------------------------------------------------------------ //
//                                     WHERE                                      //
// ------------------------------------------------------------------------------ //
WhereClause: Box<ast::Expr> = { "WHERE" <ExprQuery> }

// ------------------------------------------------------------------------------ //
//                                   GROUP BY                                     //
// ------------------------------------------------------------------------------ //
GroupClause: Box<ast::AstNode<ast::GroupByExpr>> = {
    <lo:@L> "GROUP" <strategy: GroupStrategy> "BY" <keys:CommaSepPlus<GroupKey>> <group_as_alias:GroupAlias?> <hi:@R> => {
        Box::new(state.node(ast::GroupByExpr{
            strategy,
            keys,
            group_as_alias,
        }, lo..hi))
    }
}
#[inline]
GroupStrategy: ast::GroupingStrategy = {
    "ALL" => ast::GroupingStrategy::GroupFull,
    <partial:"PARTIAL"?> => {
        match partial {
            Some(_) => ast::GroupingStrategy::GroupPartial,
            None => ast::GroupingStrategy::GroupFull,
        }
    }
}
#[inline]
GroupKey: ast::AstNode<ast::GroupKey> = {
    <lo:@L> <expr:ExprQuery> <hi:@R>
        => state.node(ast::GroupKey{ expr, as_alias: None }, lo..hi),
    <lo:@L> <expr:ExprQuery> "AS" <as_alias:SymbolPrimitive> <hi:@R>
        => state.node(ast::GroupKey{ expr, as_alias: Some(as_alias) }, lo..hi),
}
#[inline]
GroupAlias: ast::SymbolPrimitive = {
    "GROUP" "AS" <SymbolPrimitive>
}

// ------------------------------------------------------------------------------ //
//                                    HAVING                                      //
// ------------------------------------------------------------------------------ //
HavingClause: Box<ast::Expr> = { "HAVING" <ExprQuery> }

// ------------------------------------------------------------------------------ //
//                                   ORDER BY                                     //
// ------------------------------------------------------------------------------ //
OrderByClause: Box<ast::AstNode<ast::OrderByExpr>> = {
    <lo:@L> "ORDER" "BY" "PRESERVE" <hi:@R> => Box::new( state.node(ast::OrderByExpr{ sort_specs: vec![] }, lo..hi) ),
    <lo:@L> "ORDER" "BY" <sort_specs: CommaSepPlus<OrderSortSpec>> <hi:@R> => Box::new( state.node(ast::OrderByExpr{ sort_specs }, lo..hi) ),
}
#[inline]
OrderSortSpec: ast::AstNode<ast::SortSpec> = {
    <lo:@L> <expr:ExprQuery> <ordering_spec:BySpec?> <null_ordering_spec:ByNullSpec?> <hi:@R>
        => state.node(ast::SortSpec { expr, ordering_spec, null_ordering_spec }, lo..hi)
}
#[inline]
BySpec: ast::OrderingSpec = {
    "ASC" => ast::OrderingSpec::Asc,
    "DESC" => ast::OrderingSpec::Desc,
}
#[inline]
ByNullSpec: ast::NullOrderingSpec = {
    "NULLS" "FIRST" => ast::NullOrderingSpec::First,
    "NULLS" "LAST" => ast::NullOrderingSpec::Last,
}

// ------------------------------------------------------------------------------ //
//                                     LIMIT                                      //
// ------------------------------------------------------------------------------ //
LimitClause: Box<ast::Expr> = { "LIMIT" <ExprQuery> }

// ------------------------------------------------------------------------------ //
//                                     OFFSET                                     //
// ------------------------------------------------------------------------------ //
OffsetByClause: Box<ast::Expr> = { "OFFSET" <ExprQuery> }

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                   Expr Query                                   //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Note: the `Expr` rules are formulated in a precise way to assure 'natural'
//    interpretation of mathematical expressions in an LR grammar while attempting
//    to stay in accordance with SQL interpretations.
//
// In particular:
//    - in order to assure operator precedence, the lowest precedence operators
//      (i.e., `OR`) are at the top of the recursion tree, and the highest
//      precedence are at the bottom (i.e., `<PathExpr>`)
//    - left-associative operators (i.e., '+', '-', '*', '/') are expressed as
//      left-self-recursive rules, while right-associative operators
//      (i.e., unary '-', '^) are expressed as right-self-recursive rules
//
// In the parsing tutorials and academic literature, you will often see the above
//     structure in the 'classical expression grammar':
//                 Expr -> Expr '+' Factor | Expr '-' Factor | Factor
//                 Factor -> Factor '*' Term | Factor '/' Term | Term
//                 Term -> number
//
// Instead of trying to come up with other synonyms to Expr/Factor/Term, the rules
//      here are named directly for their numeric level in the precedence table
//      in the form ExprPrecedence<NN>
//
// PartiQL's precedence levels:
// |-------+-------------------+---------------+------------------------------------|
// | Level | Operator          | Associativity | Description                        |
// |-------+-------------------+---------------+------------------------------------|
// |     1 | <Path Expression> | left          | e.g., `field`,  `binding.field[2]` |
// |     2 | <Function call>   | left          | e.g., `upper(field_reference)`     |
// |     3 | + -               | right         | unary plus, unary minus            |
// |     4 | ^                 | left          | exponentiation                     |
// |     5 | * / %             | left          | multiplication, division, modulo   |
// |     6 | + -               | left          | addition, subtraction              |
// |     7 | <other>           | left          | other operators, e.g., `||`
// |     8 | BETWEEN IN LIKE   |               | range/set/pattern compare          |
// |     9 | < > <= >=         |               | comparison operators               |
// |    10 | = <> !=           |               | equality operators                 |
// |    11 | IS                |               | IS [NOT] NULL                      |
// |    12 | NOT               | right         | logical negate                     |
// |    13 | AND               | left          | logical conjuct                    |
// |    14 | OR                | left          | logical disjunct                   |
// |-------+-------------------+---------------+------------------------------------|
//
// See https://en.wikipedia.org/wiki/Order_of_operations#Programming_languages
// See https://en.wikipedia.org/wiki/Order_of_operations#Special_cases


ExprQuery: Box<ast::Expr> = {
    <e:ExprPrecedence15> => Box::new(e)
}

ExprPrecedence15: ast::Expr = {
    <lo:@L> <l:ExprPrecedence15> "OR" <r:ExprPrecedence14> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Or,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence14>,
}

GraphMatch: ast::AstNode<ast::GraphMatchExpr> = {
    <lo:@L> <selector:MatchPatternSelector?> <patterns:MatchPattern> <hi:@R> => {
        state.node(ast::GraphMatchExpr{selector, patterns: vec![patterns]}, lo..hi)
    },
}

GraphMatchList: ast::AstNode<ast::GraphMatchExpr> = {
    <lo:@L> <selector:MatchPatternSelector?> <patterns:CommaSepStar<MatchPattern>> <hi:@R> => {
        state.node(ast::GraphMatchExpr{selector, patterns: patterns}, lo..hi)
    }
}

ExprPrecedence14: ast::Expr = {
    <lo:@L> <l:ExprPrecedence14> "AND" <r:ExprPrecedence13> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::And,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence13>,
}

ExprPrecedence13: ast::Expr = {
    <lo:@L> "NOT" <r:ExprPrecedence13> <hi:@R> =>
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence12>,
}

ExprPrecedence12: ast::Expr = {
    <lo:@L> <l:ExprPrecedence12> "IS" <r:ExprPrecedence11> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Is,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence12> "IS" "NOT" <r:ExprPrecedence11> <hi:@R> => {
       let is =  ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Is,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       );
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(is),
           }, lo..hi)
       )
    },
    <ExprPrecedence11>
}

ExprPrecedence11: ast::Expr = {
    <lo:@L> <l:ExprPrecedence11> "=" <r:ExprPrecedence10> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Eq,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence11> "!=" <r:ExprPrecedence10> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Ne,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence11> "<>" <r:ExprPrecedence10> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Ne,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence10>,
}

ExprPrecedence10: ast::Expr = {
    <lo:@L> <l:ExprPrecedence09> "<" <r:ExprPrecedence09> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Lt,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence09> ">" <r:ExprPrecedence09> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Gt,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence09> "<=" <r:ExprPrecedence09> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Lte,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence09> ">=" <r:ExprPrecedence09> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Gte,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence09>,
}

ExprPrecedence09: ast::Expr = {
    <lo:@L> <value:ExprPrecedence09> "BETWEEN" <from:ExprPrecedence08> "AND" <to:ExprPrecedence08> <hi:@R> =>
       ast::Expr::Between( state.node(ast::Between{ value: Box::new(value), from: Box::new(from), to: Box::new(to) }, lo..hi) ),
    <lo:@L> <value:ExprPrecedence09> "NOT" "BETWEEN" <from:ExprPrecedence08> "AND" <to:ExprPrecedence08> <hi:@R> => {
       let between = ast::Expr::Between( state.node(ast::Between{ value: Box::new(value), from: Box::new(from), to: Box::new(to) }, lo..hi) );
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(between),
           }, lo..hi)
       )
    },
    <lo:@L> <value:ExprPrecedence09> "LIKE" <pattern:ExprPrecedence08> <escape:LikeEscape?> <hi:@R> =>
       ast::Expr::Like( state.node(ast::Like{ value: Box::new(value), pattern: Box::new(pattern), escape }, lo..hi) ),
    <lo:@L> <value:ExprPrecedence09> "NOT" "LIKE" <pattern:ExprPrecedence08> <escape:LikeEscape?> <hi:@R>  => {
       let like = ast::Expr::Like( state.node(ast::Like{ value: Box::new(value), pattern: Box::new(pattern), escape }, lo..hi) );
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(like),
           }, lo..hi)
       )
    },
    <lo:@L> <l:ExprPrecedence09> "IN" <r:ExprPrecedence08> <hi:@R> =>
       ast::Expr::In( state.node(ast::In{ lhs: Box::new(l), rhs: Box::new(r) }, lo..hi) ),
    <lo:@L> <l:ExprPrecedence09> "NOT" "IN" <r:ExprPrecedence08> <hi:@R> => {
       let in_expr = ast::Expr::In( state.node(ast::In{ lhs: Box::new(l), rhs: Box::new(r) }, lo..hi) );
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(in_expr),
           }, lo..hi)
       )
    },
    <GraphMatchExpr>,
    <ExprPrecedence08>,
}

#[inline]
LikeEscape: Box<ast::Expr> = {
    "ESCAPE" <e:ExprPrecedence07> => Box::new(e)
}

#[inline]
GraphMatchExpr: ast::Expr = {
    <lo:@L> "(" <l:ExprPrecedence08> "MATCH" <r:GraphMatchList> ")" <hi:@R> => {
       ast::Expr::GraphMatch(
           state.node(ast::GraphMatch {
               expr: Box::new(l),
               graph_expr: Box::new(r),
           }, lo..hi)
       )
    }
}

#[inline]
GraphMatchExprNoParens: ast::Expr = {
    <lo:@L> <l:ExprPrecedence08> "MATCH" <r:GraphMatch> <hi:@R> =>
       ast::Expr::GraphMatch(
           state.node(ast::GraphMatch {
               expr: Box::new(l),
               graph_expr: Box::new(r),
           }, lo..hi)
       )
}

ExprPrecedence08: ast::Expr = {
    <lo:@L> <l:ExprPrecedence08> "||" <r:ExprPrecedence07> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Concat,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence07>,
}

ExprPrecedence07: ast::Expr = {
    <lo:@L> <l:ExprPrecedence07> "+" <r:ExprPrecedence06> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Add,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence07> "-" <r:ExprPrecedence06> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Neg,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence06>,
}

ExprPrecedence06: ast::Expr = {
    <lo:@L> <l:ExprPrecedence06> "*" <r:ExprPrecedence05> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Mul,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence06> "/" <r:ExprPrecedence05> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Div,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> <l:ExprPrecedence06> "%" <r:ExprPrecedence05> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Mod,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence05>,
}

ExprPrecedence05: ast::Expr = {
    <lo:@L> <l:ExprPrecedence05> "^" <r:ExprPrecedence04> <hi:@R> =>
       ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Exp,
               lhs: Box::new(l),
               rhs: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence04>,
}

ExprPrecedence04: ast::Expr = {
    <lo:@L> "+" <r:ExprPrecedence04> <hi:@R> =>
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Pos,
               expr: Box::new(r),
           }, lo..hi)
       ),
    <lo:@L> "-" <r:ExprPrecedence04> <hi:@R> =>
       ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Neg,
               expr: Box::new(r),
           }, lo..hi)
       ),
    <ExprPrecedence03>,
}

#[inline]
ExprPrecedence03: ast::Expr = {
    <casexpr:CaseExpr> => ast::Expr::Case(casexpr),
    <ExprPrecedence02>,
}

ExprPrecedence02: ast::Expr = {
    <lo:@L> <expr:PathExpr> <hi:@R> => ast::Expr::Path( state.node(expr, lo..hi) ),
    <ExprPrecedence01>,
}

PathExpr: ast::Path = {
    <l:ExprPrecedence01> "." <steps:PathSteps> => ast::Path { root:Box::new(l), steps },
    <l:ExprPrecedence01> "[" "*" "]" "." <s:PathSteps> => {
        let step = ast::PathStep::PathWildCard;
        ast::Path {
            root: Box::new(l),
            steps: std::iter::once(step).chain(s.into_iter()).collect()
        }
    },
    <l:ExprPrecedence01> "[" <expr:ExprQuery> "]" "." <s:PathSteps> => {
       let step = ast::PathStep::PathExpr(
        ast::PathExpr{
                index: Box::new(*expr),
            }
        );

        ast::Path {
            root: Box::new(l),
            steps: std::iter::once(step).chain(s.into_iter()).collect()
        }
    },
    <l:ExprPrecedence01> "[" "*" "]" => ast::Path {
        root:Box::new(l), steps:vec![ast::PathStep::PathWildCard]
    },
    <l:ExprPrecedence01> "[" <expr:ExprQuery> "]" => {
         let step = ast::PathStep::PathExpr(
             ast::PathExpr{
                 index: Box::new(*expr),
             });

        ast::Path {
            root:Box::new(l), steps:vec![step]
        }
    },
}

#[inline]
ExprPrecedence01: ast::Expr = {
    <lo:@L> <call:FunctionCall> <hi:@R> => ast::Expr::Call( state.node(call, lo..hi) ),
    <ExprTerm>,
}

ExprTerm: ast::Expr = {
    "(" <q:Query> ")" =>  *strip_query(q),
    <lo:@L> <lit:Literal> <hi:@R> => ast::Expr::Lit( state.node(lit, lo..hi) ),
    <VarRefExpr>,
    <ExprTermCollection>,
    <ExprTermTuple>,
    ! => { state.errors.push(<>); ast::Expr::Error},
}



// ------------------------------------------------------------------------------ //
//                                                                                //
//                               GPML Expression                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //

//#[inline]
MatchPatternSelector: ast::GraphMatchSelector = {
   "ANY" "SHORTEST" => ast::GraphMatchSelector::AnyShortest,
   "ALL" "SHORTEST" => ast::GraphMatchSelector::AllShortest,
   "ANY" <k:"Int"?> => {
        // TODO handle bad number parse
        k.map(|n| ast::GraphMatchSelector::AnyK(n.parse().unwrap())).unwrap_or(ast::GraphMatchSelector::Any)
   },
   "SHORTEST" <k:"Int"> => {
        // TODO handle bad number parse
        ast::GraphMatchSelector::ShortestK(k.parse().unwrap())
   },
   "SHORTEST" <k:"Int"> "GROUP" => {
        // TODO handle bad number parse
        ast::GraphMatchSelector::ShortestKGroup(k.parse().unwrap())
   }
}

MatchPattern: ast::AstNode<ast::GraphMatchPattern> = {
    <lo:@L> <restrictor:MatchPatternRestrictor?> <variable:MatchPatternPathVariable?> <parts:MatchPatternParts>  <hi:@R> => {
        state.node(ast::GraphMatchPattern{
            restrictor,
            quantifier: None,
            prefilter: None,
            variable,
            parts,
        }, lo..hi)
    },
    <lo:@L> "(" <pattern:MatchPattern> <prefilter:MatchPatternPartPrefilter?> ")" <quantifier:MatchPatternQuantifier?> <hi:@R> => {
         state.node(ast::GraphMatchPattern{
             quantifier,
             prefilter,
             ..pattern.node
         }, lo..hi)
    },
    <lo:@L> "[" <pattern:MatchPattern> <prefilter:MatchPatternPartPrefilter?> "]" <quantifier:MatchPatternQuantifier?> <hi:@R> => {
         state.node(ast::GraphMatchPattern{
             quantifier,
             prefilter,
             ..pattern.node
         }, lo..hi)
    },
}

MatchPatternNested: ast::AstNode<ast::GraphMatchPattern> = {
  <lo:@L> <restrictor:MatchPatternRestrictor?> <variable:MatchPatternPathVariable?> <parts:MatchPatternPartsNested> <hi:@R> => {
    state.node(ast::GraphMatchPattern{
        restrictor,
        quantifier: None,
        prefilter: None,
        variable,
        parts,
    }, lo..hi)
 }
}

//#[inline]
MatchPatternRestrictor: ast::GraphMatchRestrictor = {
   "TRAIL" => ast::GraphMatchRestrictor::Trail,
   "ACYCLIC" => ast::GraphMatchRestrictor::Acyclic,
   "SIMPLE" => ast::GraphMatchRestrictor::Simple,
}

//#[inline]
MatchPatternParts: Vec<ast::GraphMatchPatternPart> = {
   <n:MatchPatternPartNode> <parts:MatchPatternPartContinue*> => {
        let node = ast::GraphMatchPatternPart::Node(n);
        std::iter::once(node).chain(parts.into_iter().flatten()).collect()
   }
}

//#[inline]
MatchPatternPartsNested: Vec<ast::GraphMatchPatternPart> = {
   <MatchPatternParts>,
   <MatchPatternPartContinue>,
   <e:MatchPatternPartEdge> => vec![ast::GraphMatchPatternPart::Edge(e)],
}

MatchPatternPartContinue: Vec<ast::GraphMatchPatternPart> = {
   <e:MatchPatternPartEdge> <n:MatchPatternPartNode> => vec![ast::GraphMatchPatternPart::Edge(e),ast::GraphMatchPatternPart::Node(n)],
   <p:MatchPatternPartParen> <n:MatchPatternPartNode> => vec![ast::GraphMatchPatternPart::Pattern(p),ast::GraphMatchPatternPart::Node(n)],
}

MatchPatternPartParen: ast::AstNode<ast::GraphMatchPattern> = {
    <lo:@L> "(" <pattern:MatchPatternNested> <prefilter:MatchPatternPartPrefilter?> ")" <quantifier:MatchPatternQuantifier?> <hi:@R> => {
           state.node(ast::GraphMatchPattern {
                prefilter,
                quantifier,
                ..pattern.node
           }, lo..hi)
   },
    <lo:@L> "[" <pattern:MatchPatternNested> <prefilter:MatchPatternPartPrefilter?> "]" <quantifier:MatchPatternQuantifier?> <hi:@R> => {
            state.node(ast::GraphMatchPattern {
                 prefilter,
                 quantifier,
                 ..pattern.node
            }, lo..hi)
    },
}

//#[inline]
MatchPatternQuantifier: ast::AstNode<ast::GraphMatchQuantifier> = {
   <lo:@L> "+" <hi:@R> => state.node(ast::GraphMatchQuantifier{ lower:0, upper:None }, lo..hi),
   <lo:@L> "*" <hi:@R> => state.node(ast::GraphMatchQuantifier{ lower:1, upper:None }, lo..hi),
   <lo:@L> "{" <lower:"Int"> "," <upper:"Int"?> "}" <hi:@R> => {
 	     // TODO error on invalid literal
         state.node(ast::GraphMatchQuantifier{ lower: lower.parse().unwrap(), upper: upper.map(|n| n.parse().unwrap()) }, lo..hi)
   },
}

MatchPatternPartNode: ast::AstNode<ast::GraphMatchNode> = {
   <lo:@L> "(" <variable:MatchPatternPartName?> <label:MatchPatternPartLabel?> <prefilter:MatchPatternPartPrefilter?> ")" <hi:@R> => {
       state.node(ast::GraphMatchNode {
            prefilter,
            variable,
            label,
       }, lo..hi)
   },
}

MatchPatternPartEdge: ast::AstNode<ast::GraphMatchEdge> = {
   <lo:@L> <spec:MatchPatternPartEdgeAbbr> <quantifier:MatchPatternQuantifier?> <hi:@R>  => state.node(ast::GraphMatchEdge{ quantifier, ..spec}, lo..hi),
   <lo:@L> <spec:MatchPatternPartEdgeWSpec> <quantifier:MatchPatternQuantifier?> <hi:@R> => state.node(ast::GraphMatchEdge{ quantifier, ..spec}, lo..hi),
}

MatchPatternPartEdgeWSpec: ast::GraphMatchEdge = {
   "-" <spec:MatchPatternPartEdgeSpec> "-" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Right, ..spec},
   "~" <spec:MatchPatternPartEdgeSpec> "~" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Undirected, ..spec},
   "<" "-" <spec:MatchPatternPartEdgeSpec> "-" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Left, ..spec},
   "~" <spec:MatchPatternPartEdgeSpec> "~" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::UndirectedOrRight, ..spec},
   "<" "~" <spec:MatchPatternPartEdgeSpec> "~" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrUndirected, ..spec},
   "<" "-" <spec:MatchPatternPartEdgeSpec> "-" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrRight, ..spec},
   "-" <spec:MatchPatternPartEdgeSpec> "-" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrUndirectedOrRight, ..spec},
}

MatchPatternPartEdgeSpec: ast::GraphMatchEdge = {
   "[" <variable:MatchPatternPartName?> <label:MatchPatternPartLabel?> <prefilter:MatchPatternPartPrefilter?> "]" => {
       ast::GraphMatchEdge {
            direction: ast::GraphMatchDirection::Undirected,
            quantifier: None,
            prefilter,
            variable,
            label,
       }
    }
}

MatchPatternPartEdgeAbbr: ast::GraphMatchEdge = {
   "-" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Right, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "~" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Undirected, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "<" "-" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::Left, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "~" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::UndirectedOrRight, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "<" "~" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrUndirected, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "<" "-" ">" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrRight, quantifier: None, prefilter: None, variable: None, label: Default::default() },
   "-" => ast::GraphMatchEdge{ direction: ast::GraphMatchDirection::LeftOrUndirectedOrRight, quantifier: None, prefilter: None, variable: None, label: Default::default() },
}

//#[inline]
MatchPatternPartName: ast::SymbolPrimitive = {
    <name:"UnquotedIdent"> => {
        ast::SymbolPrimitive {
            value: name.to_owned(),
            case: ast::CaseSensitivity::CaseSensitive
        }
    }
}

//#[inline] // TODO conjunction/disjunction/negation
MatchPatternPartLabel: Vec<ast::SymbolPrimitive> = {
    ":" <l:MatchPatternPartName> => vec![l]
}

//#[inline]
MatchPatternPartPrefilter: Box<ast::Expr> = {
    "WHERE" <ExprQuery>
}

//#[inline]
MatchPatternPathVariable: ast::SymbolPrimitive = {
    <MatchPatternPartName> "="
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                               Case Expression                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //
// Implements parsing for CASE expressions.
//
// Searched Case Example:
// CASE WHEN titanId IS 1 THEN 2 WHEN titanId IS 2 THEN 3 ELSE 1 END
//
// Simple Case Example:
// CASE hello WHEN titanId IS NOT NULL THEN (SELECT * FROM data) ELSE 1 END
//
// The following is not allowed:
// CASE hello WHEN titanId IS NOT NULL THEN SELECT * FROM data ELSE 1 END
//
// This becaue as per SQL-92 standard THEN <result> ultimately leads to
// the following:
// <subquery> ::= <left paren> <query expression> <right paren>

CaseExpr: ast::AstNode<ast::Case> = {
    <lo:@L> "CASE" <expr:ExprQuery?> <cases:ExprPairWhenThen+> <elsexpr:ElseClause?> "END" <hi:@R> => {
        match expr {
            None => state.node(ast::Case::SearchedCase(
                                    ast::SearchedCase { cases, default: elsexpr }
                               ), lo..hi),
            Some(expr) => state.node(ast::Case::SimpleCase(
                                    ast::SimpleCase { expr, cases, default: elsexpr }
                                    ), lo..hi)
        }
    }
}

ElseClause: Box<ast::Expr> = {
  "ELSE" <e:ExprQuery> => Box::new(*e)
}

ExprPairWhenThen: ast::ExprPair = {
    <lo:@L> "WHEN" <first:ExprQuery> "THEN" <second:ExprQuery> <hi:@R> => ast::ExprPair { first, second },
}

#[inline]
ExprTermCollection: ast::Expr = {
      <ExprTermArray>,
      <ExprTermBag>,
}

#[inline]
ExprTermArray: ast::Expr = {
    <ExprTermArrayBrackets>,
    <ExprTermArrayParens>
}

#[inline]
ExprTermArrayBrackets: ast::Expr = {
    <lo:@L> "[" <values:CommaTermStar<ExprQuery>> "]" <hi:@R> => ast::Expr::List( state.node(ast::List{values}, lo..hi) )
}

#[inline]
ExprTermArrayParens: ast::Expr = {
    <lo:@L> "(" <values:CommaTermPlus<ExprQuery>> ")" <hi:@R> => ast::Expr::List( state.node(ast::List{values}, lo..hi) )
}

#[inline]
ExprTermBag: ast::Expr = {
    <lo:@L> "<<" <values:CommaTermStar<ExprQuery>> ">>" <hi:@R> => ast::Expr::Bag( state.node(ast::Bag{values}, lo..hi) )
}

#[inline]
ExprTermTuple: ast::Expr = {
    <lo:@L> "{" <fields:CommaTermStar<ExprPair>> "}" <hi:@R> => ast::Expr::Struct( state.node(ast::Struct{fields}, lo..hi) )
}

ExprPair: ast::ExprPair = {
    <lo:@L> <first:ExprQuery> ":" <second:ExprQuery> <hi:@R> => ast::ExprPair{ first, second },
}

#[inline]
FunctionCall: ast::Call = {
    <func_name:FunctionName> "(" <args:CommaSepStar<FunctionCallArg>> ")" =>
        ast::Call{ func_name, args }
}

#[inline]
FunctionName: ast::SymbolPrimitive = {
    <SymbolPrimitive>,
}

#[inline]
FunctionCallArg: ast::AstNode<ast::CallArg> = {
    <FunctionArgPositional>,
    <FunctionArgNamed>,
}

#[inline]
FunctionArgPositional: ast::AstNode<ast::CallArg> = {
    <lo:@L> "*" <hi:@R> => state.node(ast::CallArg::Star(), lo..hi),
    <lo:@L> <value:ExprQuery> <hi:@R> => state.node(ast::CallArg::Positional(value), lo..hi),
    <lo:@L> <ty:TypeName> <hi:@R> => state.node(ast::CallArg::PositionalType(ty), lo..hi),
}

#[inline]
FunctionArgNamed: ast::AstNode<ast::CallArg> = {
    <lo:@L> <name:FunctionArgName> ":" <value:ExprQuery> <hi:@R> => state.node(ast::CallArg::Named{name, value}, lo..hi),
    <lo:@L> <name:FunctionArgName> ":" <ty:TypeName> <hi:@R> => state.node(ast::CallArg::NamedType{name, ty}, lo..hi)
}

#[inline]
FunctionArgName: ast::SymbolPrimitive = {
    <name:"QuotedIdent"> => {
        ast::SymbolPrimitive {
            value: name.to_owned(),
            case: ast::CaseSensitivity::CaseSensitive
        }
    },
    <name:"UnquotedIdent"> => {
        ast::SymbolPrimitive {
            value: name.to_owned(),
            case: ast::CaseSensitivity::CaseInsensitive
        }
    }
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                               Path Navigation                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //
// Implementation of Path Navigation as specified by PartiQL Specification Section 4:
// https://partiql.org/assets/PartiQL-Specification.pdf
//
// Examples:
// a.b
// a.*
// a.[*]
// a[*]
// a.b.c
// "a".b
// "a"."b"
// { 'a': 1, 'b': 2 }.a
// [1, 2][1]
// foo(x, y).a
// (SELECT a from en).b
//
// See the path expression conformance tests under `partiql-tests` or parser unit-tests for more examples.
PathSteps: Vec<ast::PathStep> = {
    <path:PathSteps> "." <v:PathExprVarRef> => {
        let mut steps = path;
        steps.push(ast::PathStep::PathExpr( ast::PathExpr{ index: Box::new(v) }));
        steps
    },
    <lo:@L> <path:PathSteps> "[" "*" "]" <hi:@R> => {
        let mut steps = path;
        steps.push(ast::PathStep::PathWildCard);
        steps
    },
    <lo:@L> <path:PathSteps>  "." "*" <hi:@R> => {
         let mut steps = path;
         steps.push(ast::PathStep::PathUnpivot);
         steps
         // ast::Path{ root:path.root, steps }
    },
    <lo:@L> <path:PathSteps> "[" <expr:ExprQuery> "]" <hi:@R> => {
        let step = ast::PathStep::PathExpr(
            ast::PathExpr{
                index: Box::new(*expr),
            });

        let mut steps = path;
        steps.push(step);
        steps
    },
    "[" "*" "]" => {
        vec![ast::PathStep::PathWildCard]
    },
    "[" <expr:ExprQuery> "]" => {
        vec![ast::PathStep::PathExpr( ast::PathExpr{ index: Box::new(*expr) })]
    },
    "*" => {
        vec![ast::PathStep::PathUnpivot]
    },
    <v:PathExprVarRef> => {
        vec![ast::PathStep::PathExpr( ast::PathExpr{ index: Box::new(v) })]
    },
}

PathExprVarRef: ast::Expr = {
    <lo:@L> <s:"String"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: s.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <VarRefExpr>,
}

VarRefExpr: ast::Expr = {
    <lo:@L> <ident:"UnquotedIdent"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <lo:@L> <ident:"QuotedIdent"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseSensitive },
            qualifier: ast::ScopeQualifier::Unqualified
      }, lo..hi)),
    <lo:@L> <ident:"UnquotedAtIdentifier"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <lo:@L> <ident:"QuotedAtIdentifier"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseSensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        },lo..hi)),
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                Literal Values                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //

Literal: ast::Lit = {
    <LiteralAbsent>,
    <LiteralScalar>,
    <LiteralIon>,
    <TypedLiteral>,
}

#[inline]
LiteralAbsent: ast::Lit = {
    "NULL" => ast::Lit::Null,
    "MISSING" => ast::Lit::Missing,
}
#[inline]
LiteralScalar: ast::Lit = {
    <LiteralString>,
    <LiteralBool>,
    <LiteralNumber>
}
#[inline]
LiteralString: ast::Lit = {
    <s:"String"> => ast::Lit::CharStringLit(s.to_owned()),
}
#[inline]
LiteralBool: ast::Lit = {
    "TRUE" => ast::Lit::BoolLit(true),
    "FALSE" => ast::Lit::BoolLit(false),
}
#[inline]
LiteralNumber: ast::Lit = {
    // TODO check bounds before parsing?

    <lo:@L> <n:"Int"> <hi:@R> =>? {
        n.parse()
	        .map(|n| ast::Lit::Int64Lit(n))
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
    <lo:@L> <r:"Real"> <hi:@R> =>? {
        rust_decimal::Decimal::from_str(r)
	        .map(|n| ast::Lit::DecimalLit(n))
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
    <lo:@L> <r:"ExpReal"> <hi:@R> =>? {
        rust_decimal::Decimal::from_scientific(r)
	        .map(|n| ast::Lit::DecimalLit(n))
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
}
#[inline]
LiteralIon: ast::Lit = {
    <ion:"Ion"> => ast::Lit::IonStringLit(ion.to_owned()),
}

#[inline]
TypeKeywordStr: &'static str = {
    "DATE" => "DATE",
    "TIME" => "TIME",
    "TIMESTAMP" => "TIMESTAMP",
    "WITH" => "WITH",
    "WITHOUT" => "WITHOUT",
    "ZONE" => "ZONE",
}

#[inline]
TypeKeyword: ast::SymbolPrimitive = {
    <s:TypeKeywordStr> => ast::SymbolPrimitive { value: s.to_string(), case: ast::CaseSensitivity::CaseInsensitive, },
}

#[inline]
TypeParam: ast::CustomTypeParam = {
    <lo:@L> <ty:TypeName> <hi:@R> => ast::CustomTypeParam::Type(ty),
    <lo:@L> <lit:LiteralScalar> <hi:@R> => ast::CustomTypeParam::Lit(lit),
}

TypeNamePart: ast::CustomTypePart = {
    <id:TypeKeyword> => ast::CustomTypePart::Name( id ),
    <id:TypeKeyword> "(" <args:CommaSepPlus<TypeParam>> ")" =>  ast::CustomTypePart::Parameterized( id, args ),
}

#[inline]
TypeName: ast::Type = {
    <parts:TypeNamePart+> => ast::Type::CustomType( ast::CustomType{ parts: parts } ),
}

#[inline]
TypedLiteral: ast::Lit = {
    <ty:TypeName> <s:"String"> => ast::Lit::TypedLit(s.to_owned(), ty),
    // TODO we could support postgres-style literals with the following:
    //<s:"String"> "::" <ty:TypeName> => ast::Lit::TypedLit(s.to_owned(), ty),
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                  Utilities                                     //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Comma as terminator (i.e. "<T>, <T>, <T>," where final comma is optional); may be empty
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaTermStar<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

// Comma as terminator (i.e. "<T>, <T>, <T>," where final comma is optional); at least 1 arg
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaTermPlus<T>: Vec<T> = {
    <mut v:(<T> ",")+> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

// Comma as separator (i.e. "<T>, <T>, <T>"); may be empty
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaSepStar<T>: Vec<T> = {
    <e:T?> <mut v:("," <T>)*> => match e {
         None => vec![],
         Some(e) => {
             v.insert(0,e);
             v
        }
    }
}

// Comma as separator (i.e. "<T>, <T>, <T>"); at least 1 arg
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaSepPlus<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
}

SymbolPrimitive: ast::SymbolPrimitive = {
    <ident:"UnquotedIdent"> => ast::SymbolPrimitive {
        value: ident.to_owned(),
        case: ast::CaseSensitivity::CaseInsensitive,
    },
    <ident:"QuotedIdent"> => ast::SymbolPrimitive {
        value: ident.to_owned(),
        case: ast::CaseSensitivity::CaseSensitive,
    },
}

AsIdent: ast::SymbolPrimitive = {
    "AS" <SymbolPrimitive>
}

AtIdent: ast::SymbolPrimitive = {
    "AT" <SymbolPrimitive>
}

ByIdent: ast::SymbolPrimitive = {
    "BY" <SymbolPrimitive>
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                    Lexer                                       //
//                                                                                //
// ------------------------------------------------------------------------------ //

// The lexer is external; See [`lexer.rs`] for its definition.
//
// See also http://lalrpop.github.io/lalrpop/lexer_tutorial/002_writing_custom_lexer.html
extern {
    type Location = ByteOffset;
    type Error = ParseError<'input, BytePosition>;

    enum lexer::Token<'input> {

        // Brackets
        "[" => lexer::Token::OpenSquare,
        "]" => lexer::Token::CloseSquare,
        "{" => lexer::Token::OpenCurly,
        "}" => lexer::Token::CloseCurly,
        "(" => lexer::Token::OpenParen,
        ")" => lexer::Token::CloseParen,
        "<<" => lexer::Token::OpenDblAngle,
        ">>" => lexer::Token::CloseDblAngle,

        // Punc
        "," => lexer::Token::Comma,
        ":" => lexer::Token::Colon,
        ";" => lexer::Token::Semicolon,
        "." => lexer::Token::Period,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        "*" => lexer::Token::Star,
        "/" => lexer::Token::Slash,
        "%" => lexer::Token::Percent,
        "^" => lexer::Token::Caret,
        "||" => lexer::Token::DblPipe,

        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        "!=" => lexer::Token::BangEqual,
        "<>" => lexer::Token::LessGreater,
        "~" => lexer::Token::Tilde,

        "<" => lexer::Token::LessThan,
        ">" => lexer::Token::GreaterThan,
        "<=" => lexer::Token::LessEqual,
        ">=" => lexer::Token::GreaterEqual,


        // Types
        "UnquotedIdent" => lexer::Token::UnquotedIdent(<&'input str>),
        "QuotedIdent" => lexer::Token::QuotedIdent(<&'input str>),
        "UnquotedAtIdentifier" => lexer::Token::UnquotedAtIdentifier(<&'input str>),
        "QuotedAtIdentifier" => lexer::Token::QuotedAtIdentifier(<&'input str>),
        "Int" => lexer::Token::Int(<&'input str>),
        "Real" => lexer::Token::Real(<&'input str>),
        "ExpReal" => lexer::Token::ExpReal(<&'input str>),
        "String" => lexer::Token::String(<&'input str>),
        "Ion" => lexer::Token::Ion(<&'input str>),

        // Keywords
        "ACYCLIC" => lexer::Token::Acyclic,
        "ALL" => lexer::Token::All,
        "ASC" => lexer::Token::Asc,
        "AND" => lexer::Token::And,
        "ANY" => lexer::Token::Any,
        "AS" => lexer::Token::As,
        "AT" => lexer::Token::At,
        "BETWEEN" => lexer::Token::Between,
        "BY" => lexer::Token::By,
        "CASE" => lexer::Token::Case,
        "CROSS" => lexer::Token::Cross,
        "DATE" => lexer::Token::Date,
        "DESC" => lexer::Token::Desc,
        "DISTINCT" => lexer::Token::Distinct,
        "ELSE" => lexer::Token::Else,
        "END" => lexer::Token::End,
        "ESCAPE" => lexer::Token::Escape,
        "EXCEPT" => lexer::Token::Except,
        "FALSE" => lexer::Token::False,
        "FIRST" => lexer::Token::First,
        "FOR" => lexer::Token::For,
        "FULL" => lexer::Token::Full,
        "FROM" => lexer::Token::From,
        "GROUP" => lexer::Token::Group,
        "HAVING" => lexer::Token::Having,
        "IN" => lexer::Token::In,
        "INNER" => lexer::Token::Inner,
        "INTERSECT" => lexer::Token::Intersect,
        "IS" => lexer::Token::Is,
        "JOIN" => lexer::Token::Join,
        "LAST" => lexer::Token::Last,
        "LATERAL" => lexer::Token::Lateral,
        "LEFT" => lexer::Token::Left,
        "LIKE" => lexer::Token::Like,
        "LIMIT" => lexer::Token::Limit,
        "MATCH" => lexer::Token::Match,
        "MISSING" => lexer::Token::Missing,
        "NATURAL" => lexer::Token::Natural,
        "NOT" => lexer::Token::Not,
        "NULL" => lexer::Token::Null,
        "NULLS" => lexer::Token::Nulls,
        "OFFSET" => lexer::Token::Offset,
        "ON" => lexer::Token::On,
        "OR" => lexer::Token::Or,
        "ORDER" => lexer::Token::Order,
        "OUTER" => lexer::Token::Outer,
        "PARTIAL" => lexer::Token::Partial,
        "PIVOT" => lexer::Token::Pivot,
        "PRESERVE" => lexer::Token::Preserve,
        "RIGHT" => lexer::Token::Right,
        "SELECT" => lexer::Token::Select,
        "TABLE" => lexer::Token::Table,
        "TIME" => lexer::Token::Time,
        "TIMESTAMP" => lexer::Token::Timestamp,
        "SIMPLE" => lexer::Token::Simple,
        "SHORTEST" => lexer::Token::Shortest,
        "THEN" => lexer::Token::Then,
        "TRAIL" => lexer::Token::Trail,
        "TRUE" => lexer::Token::True,
        "UNION" => lexer::Token::Union,
        "UNPIVOT" => lexer::Token::Unpivot,
        "USING" => lexer::Token::Using,
        "VALUE" => lexer::Token::Value,
        "VALUES" => lexer::Token::Values,
        "WHEN" => lexer::Token::When,
        "WHERE" => lexer::Token::Where,
        "WITH" => lexer::Token::With,
        "WITHOUT" => lexer::Token::Without,
        "ZONE" => lexer::Token::Zone,
    }
}
