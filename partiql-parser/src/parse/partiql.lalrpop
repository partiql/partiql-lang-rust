use crate::parse::lexer;
use crate::error::ParseError;

use lalrpop_util as lpop;

use std::str::FromStr;

use partiql_ast::ast;

use partiql_source_map::location::{ByteOffset, BytePosition, Location, ToLocated};

use crate::parse::parse_util::{strip_expr, strip_query, strip_query_set, CallSite, Attrs, Synth};
use crate::parse::parser_state::{ParserState, IdGenerator};

grammar<'input, 'state, Id>(input: &'input str, state: &'state mut ParserState<'input, Id>) where Id: IdGenerator;


pub(crate) TopLevelQuery: ast::AstNode<ast::TopLevelQuery> = {
    <lo:@L>
    <with:WithClause?>
    <query:Query>
    <hi:@R> => {
        state.node(ast::TopLevelQuery { with, query }, lo..hi)
    }
}

Query: ast::AstNode<ast::Query> = {
    <lo:@L>
    <set:QuerySet>
    <order_by:OrderByClause?>
    <limit_offset:LimitOffsetClause>
    <hi:@R> => {
        state.node(ast::Query { set, order_by, limit_offset }, lo..hi)
    }
}


// ------------------------------------------------------------------------------ //
//                                     WITH                                       //
// ------------------------------------------------------------------------------ //
WithClause: ast::AstNode<ast::WithClause> = {
    <lo:@L> "WITH" <rec:"RECURSIVE"?> <withs:WithList> <hi:@R> => {
        let recursive = matches!(rec, Some(_));
        state.node(ast::WithClause {
             recursive,
             withs
        }, lo..hi)
    }
}

#[inline]
WithList: Vec<ast::AstNode<ast::WithElement>> = {
    <CommaSepPlus<WithListElement>>
}

#[inline]
WithListElement: ast::AstNode<ast::WithElement> = {
    <lo:@L> <query_name: SymbolPrimitive> <columns:WithColList?> "AS" <subquery:SubQueryAst> <how:WithSearchOrCycle?> <hi:@R> => {
        state.node(ast::WithElement {
             query_name,
             columns,
             subquery,
        }, lo..hi)
    }
}

#[inline]
WithColList: Vec<ast::SymbolPrimitive> = {
    "(" <CommaSepPlus<SymbolPrimitive>> ")"
}

// TODO
#[inline]
WithSearchOrCycle: () = {
    <WithSearchClause>,
    <WithCycleClause>,
    <WithSearchClause> <WithCycleClause>,
}
// TODO
WithSearchClause : () = {
    "SEARCH"
}
// TODO
WithCycleClause : () = {
    "CYCLE"
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                            UNION/INTERSECT/EXCEPT                              //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Note: the Set operation rules are formulated in a precise way to assure 'natural'
//    interpretation of set operations in an LR grammar while attempting
//    to stay in accordance with SQL interpretations.
//
// In particular:
//    - in order to assure operator precedence, the lowest precedence operators
//      (i.e., `UNION` & `EXCEPT`) are at the top of the recursion tree, and the highest
//      precedence are at the bottom (i.e., `INTERSECT`)
//    - all set operations are left-associative and are thus expressed as left-self-recursive rules

QuerySet: ast::AstNode<ast::QuerySet> = {
    <lo:@L> <lhs:Query> <bag_op:BagOp> <setq:SetQuantifierStrategy> <rhs:SingleQuery> <hi:@R> => {
        let lhs = strip_query(lhs);
        let rhs = strip_query_set(rhs, state, lo, hi);
        let bag_expr = state.node(ast::BagOpExpr {
             bag_op,
             setq,
             lhs: Box::new(lhs),
             rhs: Box::new(rhs)
        }, lo..hi);
        state.node(ast::QuerySet::BagOp(Box::new( bag_expr )), lo..hi)
	},
    <SingleQuery>,
}

#[inline]
BagOp: ast::BagOperator = {
    "UNION" => ast::BagOperator::Union,
    "OUTER" "UNION" => ast::BagOperator::OuterUnion,
    "EXCEPT" => ast::BagOperator::Except,
    "OUTER" "EXCEPT" => ast::BagOperator::OuterExcept,
    "INTERSECT" => ast::BagOperator::Intersect,
    "OUTER" "INTERSECT" => ast::BagOperator::OuterIntersect,

}

#[inline]
SetQuantifier: ast::SetQuantifier = {
    "DISTINCT"? => ast::SetQuantifier::Distinct,
    "ALL" => ast::SetQuantifier::All,
}



// ------------------------------------------------------------------------------ //
//                                                                                //
//                            ExprQuery or SFW Query                              //
//                                                                                //
// ------------------------------------------------------------------------------ //
SingleQuery: ast::AstNode<ast::QuerySet> = {
    <lo:@L> <expr:ExprQuery> <hi:@R> => {
        match *expr {
           ast::Expr::Query(ast::AstNode{ node: ast::Query{set, order_by:None, limit_offset:None} , .. }) => set,
           _ => state.node(ast::QuerySet::Expr( expr ), lo..hi),
        }
    },
    <lo:@L> <sfw:SfwQuery> <hi:@R> => state.node(ast::QuerySet::Select( Box::new(sfw)), lo..hi),
    <lo:@L> <values:Values> <hi:@R> => values,
    <lo:@L> <table:ExplicitTable> <hi:@R> => table,
}

Values: ast::AstNode<ast::QuerySet> = {
    <lo:@L> "VALUES" <rows:CommaSepPlus<ValueRow>> <hi:@R> => state.node(ast::QuerySet::Values( rows ), lo..hi)
}

#[inline]
ValueRow: Box<ast::Expr> = {
    "(" <e:ExprQuery> ")" => Box::new(*e),
    <array:ExprTermCollection> => Box::new(array.data)
}

#[inline]
ExplicitTable: ast::AstNode<ast::QuerySet> = {
    <lo:@L> "TABLE" <table_name:SymbolPrimitive> <hi:@R> => state.node(ast::QuerySet::Table( ast::QueryTable{table_name} ), lo..hi)
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                   SFW Query                                    //
//                                                                                //
// ------------------------------------------------------------------------------ //
SfwQuery: ast::AstNode<ast::Select> = {
    <SfwClauses>,
    <FwsClauses>
}

// SQL-style where `Select` precedes `From`
SfwClauses: ast::AstNode<ast::Select> = {
    <lo:@L>
    <project:SelectClause>
    <from:FromClause?>
    <where_clause:WhereClause?>
    <group_by:GroupClause?>
    <having:HavingClause?>
    <hi:@R> => {
        state.node(ast::Select {
            project,
            from,
            from_let: None,
            where_clause,
            group_by,
            having,
        }, lo..hi)
    }
}

// PartiQL-style where `Select` is last
FwsClauses: ast::AstNode<ast::Select> = {
    <lo:@L>
    <from:FromClause>
    <where_clause:WhereClause?>
    <group_by:GroupClause?>
    <having:HavingClause?>
    <project:SelectClause>
    <hi:@R> => {
        state.node(ast::Select {
            project,
            from: Some(from),
            from_let: None,
            where_clause,
            group_by,
            having,
        }, lo..hi)
    }
}

// ------------------------------------------------------------------------------ //
//                                    SELECT                                      //
// ------------------------------------------------------------------------------ //
SelectClause: ast::AstNode<ast::Projection> = {
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> "*" <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectStar,
        setq: strategy,
    }, lo..hi),
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> <project_items:CommaSepPlus<Projection>> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectList(project_items),
        setq: strategy,
    }, lo..hi),
    <lo:@L> "SELECT" <strategy: SetQuantifierStrategy> "VALUE" <value:ExprQuery> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectValue(value),
        setq: strategy,
    }, lo..hi),
    <lo:@L> "PIVOT" <value:ExprQuery> "AT" <key:ExprQuery> <hi:@R> => state.node(ast::Projection {
        kind: ast::ProjectionKind::ProjectPivot(ast::ProjectPivot { key, value }),
        setq: None,
    }, lo..hi),
}

#[inline]
SetQuantifierStrategy: Option<ast::SetQuantifier> = {
    "ALL" => Some(ast::SetQuantifier::All),
    <distinct: "DISTINCT"?> => distinct.map(|_|ast::SetQuantifier::Distinct)
}

#[inline]
Projection: ast::AstNode<ast::ProjectItem> = {
    <lo:@L> <expr:ExprQuery> <hi:@R>
        => state.node(ast::ProjectItem::ProjectExpr( ast::ProjectExpr{ expr, as_alias: None } ), lo..hi),
    <lo:@L> <expr:ExprQuery> "AS"? <as_alias:SymbolPrimitive> <hi:@R> => {
        state.node(ast::ProjectItem::ProjectExpr( ast::ProjectExpr{ expr, as_alias: Some(as_alias) } ), lo..hi)
    },
}

// ------------------------------------------------------------------------------ //
//                                     FROM                                       //
// ------------------------------------------------------------------------------ //
FromClause: ast::AstNode<ast::FromClause> = {
    <lo:@L> "FROM" <mut froms:(<TableReference> "," "LATERAL"?)*> <last:TableReference> <hi:@R> => {
        let total: Location<BytePosition> = Location::from(lo.into()..hi.into());

        // use `reduce` to process the comma-seperated `TableReference`s
        //   as left-associative `CROSS JOIN`s
        froms.push(last);
        let source = froms.into_iter()
            .reduce(|lfrom, rfrom| {
                let start_id = match &lfrom {
                    ast::FromSource::FromLet(node) => node.id,
                    ast::FromSource::Join(node) => node.id,
                };
                let end_id = match &rfrom {
                    ast::FromSource::FromLet(node) => node.id,
                    ast::FromSource::Join(node) => node.id,
                };

	            let start = state.locations.get(&start_id).unwrap_or(&total).start.0.clone();
                let end = state.locations.get(&end_id).unwrap_or(&total).end.0.clone();
                let range = start..end;
                let join = state.node(ast::Join {
                    kind: ast::JoinKind::Cross,
                    left: Box::new(lfrom),
                    right: Box::new(rfrom),
                    predicate: None
                }, range);
                ast::FromSource::Join( join )
            })
            .unwrap(); // safe, because we know there's at least 1 input
        state.node(ast::FromClause{source}, lo..hi)
    }
}

TableReference: ast::FromSource = {
    <TableNonJoin>,
    <TableJoined>,
}

TableNonJoin: ast::FromSource = {
    <lo:@L> <t:TableBaseReference> <hi:@R> =>ast::FromSource::FromLet( t ),
    <lo:@L> <t:TableUnpivot> <hi:@R> => ast::FromSource::FromLet( t ),
}

#[inline]
TableBaseReference: ast::AstNode<ast::FromLet> = {
    <lo:@L> <e:ExprQuery> <as_alias:AsIdent?> <at_alias:AtIdent?> <by_alias:ByIdent?> <hi:@R> => {
        state.node(ast::FromLet {
            expr: e,
            kind: ast::FromLetKind::Scan,
            as_alias,
            at_alias,
            by_alias
        }, lo..hi)
    }
}

#[inline]
TableUnpivot: ast::AstNode<ast::FromLet> = {
    <lo:@L> "UNPIVOT" <e:ExprQuery> <as_ident:AsIdent?> <at_ident:AtIdent?> <hi:@R> => {
        state.node(ast::FromLet {
            expr: e,
            kind: ast::FromLetKind::Unpivot,
            as_alias: as_ident,
            at_alias: at_ident,
            by_alias: None,
        }, lo..hi)
    }
}

TableJoined: ast::FromSource = {
    <TableCrossJoin>,
    <TableQualifiedJoin>,
    "(" <TableJoined> ")",
}

#[inline]
TableCrossJoin: ast::FromSource = {
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <j:JoinType?> "CROSS" KW_JOIN <rtable:JoinRhs> <hi:@R> => {
        let kind = match j {
            Some(j) => j,
            None => ast::JoinKind::Cross
        };
        let join = state.node(ast::Join {
            kind,
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: None
        }, lo..hi);
        ast::FromSource::Join( join )
    }
}

#[inline]
TableQualifiedJoin: ast::FromSource = {
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <j:JoinType?> KW_JOIN <rtable:JoinRhs> <on:JoinSpec> <hi:@R> => {
        let join = state.node(ast::Join {
            kind: j.unwrap_or(ast::JoinKind::Inner),
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: Some(on),
        }, lo..hi);
        ast::FromSource::Join( join )
    },
    // Note the `TableReference` on the lhs and the `JoinRhs` on the rhs of the `JOIN`.
    // This is to prevent ambiguity in the grammar and effectively treats `JOIN` like
    //    a left-associative operator
    <lo:@L> <ltable:TableReference> <spec:JoinSpecNatural> <j:JoinType?> KW_JOIN <rtable:JoinRhs> <hi:@R> => {
        let join = state.node(ast::Join {
            kind: j.unwrap_or(ast::JoinKind::Inner),
            left: Box::new(ltable),
            right: Box::new(rtable),
            predicate: Some(spec)
        }, lo..hi);
        ast::FromSource::Join( join )
    },
}
#[inline]
KW_JOIN: () = {
    "JOIN",
    "LATERAL",
    "JOIN" "LATERAL",
}
#[inline]
JoinRhs: ast::FromSource = {
    <TableNonJoin>,
    "(" <TableJoined> ")"
}
#[inline]
JoinSpecNatural: ast::AstNode<ast::JoinSpec> = {
    <lo:@L> "NATURAL" <hi:@R> => state.node(ast::JoinSpec::Natural, lo..hi)
}
#[inline]
JoinType: ast::JoinKind = {
    "INNER" => ast::JoinKind::Inner,
    "LEFT" "OUTER"? => ast::JoinKind::Left,
    "RIGHT" "OUTER"? => ast::JoinKind::Right,
    "FULL" "OUTER"? => ast::JoinKind::Full,
}
#[inline]
JoinSpec: ast::AstNode<ast::JoinSpec> = {
    <lo:@L> "ON" <e:ExprQuery> <hi:@R> => state.node(ast::JoinSpec::On(e), lo..hi),
    <lo:@L> "USING" "(" <paths:CommaSepPlus<PathExpr>> ")" <hi:@R> => state.node(ast::JoinSpec::Using( paths ), lo..hi),
}

// ------------------------------------------------------------------------------ //
//                                     WHERE                                      //
// ------------------------------------------------------------------------------ //
WhereClause: Box<ast::AstNode<ast::WhereClause>> = {
    <lo:@L> "WHERE" <expr:ExprQuery> <hi:@R> => {
        Box::new(state.node(ast::WhereClause{expr}, lo..hi))
    }
}

// ------------------------------------------------------------------------------ //
//                                   GROUP BY                                     //
// ------------------------------------------------------------------------------ //
GroupClause: Box<ast::AstNode<ast::GroupByExpr>> = {
    <lo:@L> "GROUP" <strategy: GroupStrategy>  <keys:GroupByKeys?> <group_as_alias:GroupAlias?> <hi:@R> => {
        let keys = keys.unwrap_or_default();
        Box::new(state.node(ast::GroupByExpr{
            strategy,
            keys,
            group_as_alias,
        }, lo..hi))
    }
}
#[inline]
GroupStrategy: Option<ast::GroupingStrategy> = {
    "ALL" => Some(ast::GroupingStrategy::GroupFull),
    <partial:"PARTIAL"?> => partial.map(|_| ast::GroupingStrategy::GroupPartial),
}
#[inline]
GroupByKeys: Vec<ast::AstNode<ast::GroupKey>> = {
    "BY" <CommaSepPlus<GroupKey>>
}
#[inline]
GroupKey: ast::AstNode<ast::GroupKey> = {
    <lo:@L> <expr:ExprQuery> <hi:@R>
        => state.node(ast::GroupKey{ expr, as_alias: None }, lo..hi),
    <lo:@L> <expr:ExprQuery> "AS" <as_alias:SymbolPrimitive> <hi:@R>
        => state.node(ast::GroupKey{ expr, as_alias: Some(as_alias) }, lo..hi),
}
#[inline]
GroupAlias: ast::SymbolPrimitive = {
    "GROUP" "AS" <SymbolPrimitive>
}

// ------------------------------------------------------------------------------ //
//                                    HAVING                                      //
// ------------------------------------------------------------------------------ //
HavingClause: Box<ast::AstNode<ast::HavingClause>> = {
    <lo:@L> "HAVING" <expr:ExprQuery> <hi:@R> => {
        Box::new(state.node(ast::HavingClause{expr}, lo..hi))
    }
}

// ------------------------------------------------------------------------------ //
//                                   ORDER BY                                     //
// ------------------------------------------------------------------------------ //
OrderByClause: Box<ast::AstNode<ast::OrderByExpr>> = {
    <lo:@L> "ORDER" "BY" "PRESERVE" <hi:@R> => Box::new( state.node(ast::OrderByExpr{ sort_specs: vec![] }, lo..hi) ),
    <lo:@L> "ORDER" "BY" <sort_specs: CommaSepPlus<OrderSortSpec>> <hi:@R> => Box::new( state.node(ast::OrderByExpr{ sort_specs }, lo..hi) ),
}
#[inline]
OrderSortSpec: ast::AstNode<ast::SortSpec> = {
    <lo:@L> <expr:ExprQuery> <ordering_spec:BySpec?> <null_ordering_spec:ByNullSpec?> <hi:@R>
        => state.node(ast::SortSpec { expr, ordering_spec, null_ordering_spec }, lo..hi)
}
#[inline]
BySpec: ast::OrderingSpec = {
    "ASC" => ast::OrderingSpec::Asc,
    "DESC" => ast::OrderingSpec::Desc,
}
#[inline]
ByNullSpec: ast::NullOrderingSpec = {
    "NULLS" "FIRST" => ast::NullOrderingSpec::First,
    "NULLS" "LAST" => ast::NullOrderingSpec::Last,
}

// ------------------------------------------------------------------------------ //
//                                LIMIT / OFFSET                                  //
// ------------------------------------------------------------------------------ //
LimitOffsetClause: Option<Box<ast::AstNode<ast::LimitOffsetClause>>> = {
 <lo:@L> <limit:LimitClause?> <offset:OffsetByClause?> <hi:@R> => {
   if limit.is_none() && offset.is_none() {
     None
   } else {
     Some(Box::new(state.node(ast::LimitOffsetClause { limit, offset }, lo..hi)))
   }
 }
}
LimitClause: Box<ast::Expr> = { "LIMIT" <ExprQuery> }
OffsetByClause: Box<ast::Expr> = { "OFFSET" <ExprQuery> }

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                   Expr Query                                   //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Note: the `Expr` rules are formulated in a precise way to assure 'natural'
//    interpretation of mathematical expressions in an LR grammar while attempting
//    to stay in accordance with SQL interpretations.
//
// In particular:
//    - in order to assure operator precedence, the lowest precedence operators
//      (i.e., `OR`) are at the top of the recursion tree, and the highest
//      precedence are at the bottom (i.e., `<PathExpr>`)
//    - left-associative operators (i.e., '+', '-', '*', '/') are expressed as
//      left-self-recursive rules, while right-associative operators
//      (i.e., unary '-', '^) are expressed as right-self-recursive rules
//
// In the parsing tutorials and academic literature, you will often see the above
//     structure in the 'classical expression grammar':
//                 Expr -> Expr '+' Factor | Expr '-' Factor | Factor
//                 Factor -> Factor '*' Term | Factor '/' Term | Term
//                 Term -> number
//
// Instead of trying to come up with other synonyms to Expr/Factor/Term, the rules
//      here are named directly for their numeric level in the precedence table
//      in the form ExprPrecedence<NN>
//
// PartiQL's precedence levels:
// |-------+-------------------+---------------+------------------------------------|
// | Level | Operator          | Associativity | Description                        |
// |-------+-------------------+---------------+------------------------------------|
// |     1 | <Path Expression> | left          | e.g., `field`,  `binding.field[2]` |
// |     2 | <Function call>   | left          | e.g., `upper(field_reference)`     |
// |     3 | + -               | right         | unary plus, unary minus            |
// |     4 | ^                 | left          | exponentiation                     |
// |     5 | * / %             | left          | multiplication, division, modulo   |
// |     6 | + -               | left          | addition, subtraction              |
// |     7 | <other>           | left          | other operators, e.g., `||`
// |     8 | BETWEEN IN LIKE   |               | range/set/pattern compare          |
// |     9 | < > <= >=         |               | comparison operators               |
// |    10 | = <> !=           |               | equality operators                 |
// |    11 | IS                |               | IS [NOT] NULL                      |
// |    12 | NOT               | right         | logical negate                     |
// |    13 | AND               | left          | logical conjuct                    |
// |    14 | OR                | left          | logical disjunct                   |
// |-------+-------------------+---------------+------------------------------------|
//
// See https://en.wikipedia.org/wiki/Order_of_operations#Programming_languages
// See https://en.wikipedia.org/wiki/Order_of_operations#Special_cases


ExprQuery: Box<ast::Expr> = {
    <e:ExprQuerySynth> => e.data,
}

ExprQuerySynth: Synth<Box<ast::Expr>> = {
    <e:ExprPrecedence15> => {
        let Synth{data, attrs} = e;
        Synth::new(Box::new(data), attrs)
    }
}

ExprPrecedence15: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence15> "OR" <r:ExprPrecedence14> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Or,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence14>,
}

ExprPrecedence14: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence14> "AND" <r:ExprPrecedence13> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::And,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence13>,
}

ExprPrecedence13: Synth<ast::Expr> = {
    <lo:@L> "NOT" <r:ExprPrecedence13> <hi:@R> =>
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence12>,
}

ExprPrecedence12: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence12> "IS" <r:ExprPrecedence11> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Is,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence12> "IS" "NOT" <r:ExprPrecedence11> <hi:@R> => {
       let is =  ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Is,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       );
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(is),
           }, lo..hi)
       ))
    },
    <ExprPrecedence11>
}

ExprPrecedence11: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence11> "=" <r:ExprPrecedence10> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Eq,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence11> "!=" <r:ExprPrecedence10> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Ne,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence11> "<>" <r:ExprPrecedence10> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Ne,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence10>,
}

ExprPrecedence10: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence09> "<" <r:ExprPrecedence09> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Lt,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence09> ">" <r:ExprPrecedence09> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Gt,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence09> "<=" <r:ExprPrecedence09> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Lte,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence09> ">=" <r:ExprPrecedence09> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Gte,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence09>,
}

ExprPrecedence09: Synth<ast::Expr> = {
    <lo:@L> <value:ExprPrecedence09> "BETWEEN" <from:ExprPrecedence08> "AND" <to:ExprPrecedence08> <hi:@R> =>
       Synth::empty(ast::Expr::Between( state.node(ast::Between{ value: Box::new(value.data), from: Box::new(from.data), to: Box::new(to.data) }, lo..hi) )),
    <lo:@L> <value:ExprPrecedence09> "NOT" "BETWEEN" <from:ExprPrecedence08> "AND" <to:ExprPrecedence08> <hi:@R> => {
       let between = ast::Expr::Between( state.node(ast::Between{ value: Box::new(value.data), from: Box::new(from.data), to: Box::new(to.data) }, lo..hi) );
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(between),
           }, lo..hi)
       ))
    },
    <lo:@L> <value:ExprPrecedence09> "LIKE" <pattern:ExprPrecedence08> <escape:LikeEscape?> <hi:@R> =>
       Synth::empty(ast::Expr::Like( state.node(ast::Like{ value: Box::new(value.data), pattern: Box::new(pattern.data), escape }, lo..hi) )),
    <lo:@L> <value:ExprPrecedence09> "NOT" "LIKE" <pattern:ExprPrecedence08> <escape:LikeEscape?> <hi:@R>  => {
       let like = ast::Expr::Like( state.node(ast::Like{ value: Box::new(value.data), pattern: Box::new(pattern.data), escape }, lo..hi) );
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(like),
           }, lo..hi)
       ))
    },
    <lo:@L> <l:ExprPrecedence09> "IN" <r:ExprPrecedence08> <hi:@R> =>
       Synth::empty(ast::Expr::In( state.node(ast::In{ lhs: Box::new(l.data), rhs: Box::new(r.data) }, lo..hi) )),
    <lo:@L> <l:ExprPrecedence09> "NOT" "IN" <r:ExprPrecedence08> <hi:@R> => {
       let in_expr = ast::Expr::In( state.node(ast::In{ lhs: Box::new(l.data), rhs: Box::new(r.data) }, lo..hi) );
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Not,
               expr: Box::new(in_expr),
           }, lo..hi)
       ))
    },
    <ExprPrecedence08>,
}

#[inline]
LikeEscape: Box<ast::Expr> = {
    "ESCAPE" <e:ExprPrecedence07> => Box::new(e.data)
}

ExprPrecedence08: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence08> "||" <r:ExprPrecedence07> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Concat,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence07>,
}

ExprPrecedence07: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence07> "+" <r:ExprPrecedence06> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Add,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence07> "-" <r:ExprPrecedence06> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Sub,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence06>,
}

ExprPrecedence06: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence06> "*" <r:ExprPrecedence05> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Mul,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence06> "/" <r:ExprPrecedence05> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Div,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> <l:ExprPrecedence06> "%" <r:ExprPrecedence05> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Mod,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence05>,
}

ExprPrecedence05: Synth<ast::Expr> = {
    <lo:@L> <l:ExprPrecedence05> "^" <r:ExprPrecedence04> <hi:@R> =>
       Synth::empty(ast::Expr::BinOp(
           state.node(ast::BinOp {
               kind: ast::BinOpKind::Exp,
               lhs: Box::new(l.data),
               rhs: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence04>,
}

ExprPrecedence04: Synth<ast::Expr> = {
    <lo:@L> "+" <r:ExprPrecedence04> <hi:@R> =>
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Pos,
               expr: Box::new(r.data),
           }, lo..hi)
       )),
    <lo:@L> "-" <r:ExprPrecedence04> <hi:@R> =>
       Synth::empty(ast::Expr::UniOp(
           state.node(ast::UniOp {
               kind: ast::UniOpKind::Neg,
               expr: Box::new(r.data),
           }, lo..hi)
       )),
    <ExprPrecedence03>,
}

#[inline]
ExprPrecedence03: Synth<ast::Expr> = {
    <casexpr:CaseExpr> => Synth::empty(ast::Expr::Case(casexpr)),
    <ExprPrecedence02>,
}

ExprPrecedence02: Synth<ast::Expr> = {
    <lo:@L> <expr:PathExpr> <hi:@R> => Synth::empty(ast::Expr::Path( state.node(expr, lo..hi) )),
    <ExprPrecedence01>,
}

PathExpr: ast::Path = {
    <l:ExprPrecedence01> <s:PathSteps> => {
        ast::Path {
            root: Box::new(l.data),
            steps: s
        }
    },
}

#[inline]
ExprPrecedence01: Synth<ast::Expr> = {
    <lo:@L> <call:FunctionCall> <hi:@R> => {
        let call = match call {
            CallSite::Call(call) => ast::Expr::Call( state.node(call, lo..hi) ),
            CallSite::CallAgg(call_agg)  => ast::Expr::CallAgg( state.node(call_agg, lo..hi) ),
        };
        Synth::empty(call)
    },
    <ExprTerm>,
}

ExprTerm: Synth<ast::Expr> = {
    <s:SubQuery> => Synth::empty(s),
    <lo:@L> <lit:Literal> <hi:@R> => Synth::new(ast::Expr::Lit( state.node(lit, lo..hi) ), Attrs::LIT),
    <v:VarRefExpr> => Synth::empty(v),
    <lo:@L> <c:ExprTermCollection> <hi:@R> => {
        if c.attrs.contains(Attrs::LIT) {
            match c.data {
                ast::Expr::List(l) => Synth::new(ast::Expr::Lit( state.node(ast::Lit::ListLit(l), lo..hi) ), Attrs::LIT),
                ast::Expr::Bag(b) => Synth::new(ast::Expr::Lit( state.node(ast::Lit::BagLit(b), lo..hi) ), Attrs::LIT),
                _ => unreachable!(),
            }
        } else {
            c
        }
    },
    <lo:@L> <t:ExprTermTuple> <hi:@R> => {
        if t.attrs.contains(Attrs::LIT) {
            match t.data {
                ast::Expr::Struct(s) => Synth::new(ast::Expr::Lit( state.node(ast::Lit::StructLit(s), lo..hi) ), Attrs::LIT),
                _ => unreachable!(),
            }
        } else {
            t
        }
    },
    ! => { state.errors.push(<>); Synth::empty(ast::Expr::Error)},
}

SubQuery: ast::Expr = {
    "(" <q:Query> ")" => *strip_expr(q),
}

SubQueryAst: ast::AstNode<ast::Expr> = {
    <lo:@L> <subq:SubQuery> <hi:@R> => state.node(subq, lo..hi),
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                               Case Expression                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //
// Implements parsing for CASE expressions.
//
// Searched Case Example:
// CASE WHEN titanId IS 1 THEN 2 WHEN titanId IS 2 THEN 3 ELSE 1 END
//
// Simple Case Example:
// CASE hello WHEN titanId IS NOT NULL THEN (SELECT * FROM data) ELSE 1 END
//
// The following is not allowed:
// CASE hello WHEN titanId IS NOT NULL THEN SELECT * FROM data ELSE 1 END
//
// This becaue as per SQL-92 standard THEN <result> ultimately leads to
// the following:
// <subquery> ::= <left paren> <query expression> <right paren>

CaseExpr: ast::AstNode<ast::Case> = {
    <lo:@L> "CASE" <expr:ExprQuery?> <cases:ExprPairWhenThen+> <elsexpr:ElseClause?> "END" <hi:@R> => {
        match expr {
            None => state.node(ast::Case::SearchedCase(
                                    ast::SearchedCase { cases, default: elsexpr }
                               ), lo..hi),
            Some(expr) => state.node(ast::Case::SimpleCase(
                                    ast::SimpleCase { expr, cases, default: elsexpr }
                                    ), lo..hi)
        }
    }
}

ElseClause: Box<ast::Expr> = {
  "ELSE" <e:ExprQuery> => Box::new(*e)
}

ExprPairWhenThen: ast::ExprPair = {
    <lo:@L> "WHEN" <first:ExprQuery> "THEN" <second:ExprQuery> <hi:@R> => ast::ExprPair { first, second },
}

#[inline]
ExprTermCollection: Synth<ast::Expr> = {
      <ExprTermArray>,
      <ExprTermBag>,
}

#[inline]
ExprTermArray: Synth<ast::Expr> = {
    <ExprTermArrayBrackets>,
    <ExprTermArrayParens>
}

#[inline]
ExprTermArrayBrackets: Synth<ast::Expr> = {
    <lo:@L> "[" <values:(<ExprQuerySynth> ",")*> <value:ExprQuerySynth?> "]" <hi:@R> => {
        let values = match value {
            None => values.into_iter().collect(),
            Some(v) => values.into_iter().chain( std::iter::once(v) ).collect(),
        };
        let Synth{data, attrs} = values;
        let data = ast::Expr::List( state.node(ast::List{values:data}, lo..hi) );
        Synth{data, attrs}
    }
}

#[inline]
ExprTermArrayParens: Synth<ast::Expr> = {
    <lo:@L> "(" <values:(<ExprQuerySynth> ",")+> <value:ExprQuerySynth?> ")" <hi:@R> => {
        let values = match value {
            None => values.into_iter().collect(),
            Some(v) => values.into_iter().chain( std::iter::once(v) ).collect(),
        };
        let Synth{data, attrs} = values;
        let data = ast::Expr::List( state.node(ast::List{values:data}, lo..hi) );
        Synth{data, attrs}
    }
}

#[inline]
ExprTermBag: Synth<ast::Expr> = {
    <lo:@L> "<<" <values:(<ExprQuerySynth> ",")*> <value:ExprQuerySynth?> ">>" <hi:@R> => {
        let values = match value {
            None => values.into_iter().collect(),
            Some(v) => values.into_iter().chain( std::iter::once(v) ).collect(),
        };
        let Synth{data, attrs} = values;
        let data = ast::Expr::Bag( state.node(ast::Bag{values:data}, lo..hi) );
        Synth{data, attrs}
    }
}

#[inline]
ExprTermTuple: Synth<ast::Expr> = {
    <lo:@L> "{" <fields:(<ExprPair> ",")*> <field:ExprPair?> "}" <hi:@R> => {
        let fields = match field {
            None => fields.into_iter().collect(),
            Some(f) => fields.into_iter().chain( std::iter::once(f) ).collect(),
        };
        let Synth{data, attrs} = fields;
        let data = ast::Expr::Struct( state.node(ast::Struct{fields:data}, lo..hi) );
        Synth{data, attrs}
    }
}

ExprPair: Synth<ast::ExprPair> = {
    <lo:@L> <first:ExprQuerySynth> ":" <second:ExprQuerySynth> <hi:@R> => {
        let Synth{data:first, attrs: fattrs} = first;
        let Synth{data:second, attrs: sattrs} = second;

        Synth{ data: ast::ExprPair{ first, second }, attrs: fattrs.synthesize(sattrs) }
    }
}

#[inline]
FunctionCall: CallSite = {
    <func_name:FunctionName> "(" <args:FunctionCallArgs> ")" => {
        if state.is_agg_fn(&func_name) {
            CallSite::CallAgg(ast::CallAgg{ func_name, args })
        } else {
            CallSite::Call(ast::Call{ func_name, args })
        }
    },
}

#[inline]
FunctionName: ast::SymbolPrimitive = {
    <SymbolPrimitive>,
}

#[inline]
FunctionCallArgs: Vec<ast::AstNode<ast::CallArg>> = {
    CommaSepStar<FunctionCallArg>,
    // Special case subquery when it is the only sub-expression of a function call (e.g., `SELECT AVG(SELECT VALUE price FROM g AS v))... `)
    <lo:@L> <subq:SfwQuery> <hi:@R> => {
        let qset = state.node(ast::QuerySet::Select(Box::new(subq)), lo..hi);
        let query = state.node(ast::Query{ set: qset, order_by: None, limit_offset:None }, lo..hi);
        vec![state.node(ast::CallArg::Positional(Box::new(ast::Expr::Query(query))), lo..hi)]
    },
}

#[inline]
FunctionCallArg: ast::AstNode<ast::CallArg> = {
    <FunctionArgPositional>,
    <FunctionArgNamed>,
}

#[inline]
FunctionArgPositional: ast::AstNode<ast::CallArg> = {
    <lo:@L> "*" <hi:@R> => state.node(ast::CallArg::Star(), lo..hi),
    <lo:@L> <expr:ExprQuery> <hi:@R> => state.node(ast::CallArg::Positional(expr), lo..hi),
    <lo:@L> <ty:TypeName> <hi:@R> => state.node(ast::CallArg::PositionalType(ty), lo..hi),
}

#[inline]
FunctionArgNamed: ast::AstNode<ast::CallArg> = {
    <lo:@L> <name:FunctionArgName> ":" <value:ExprQuery> <hi:@R> => state.node(ast::CallArg::Named(ast::CallArgNamed{name, value}), lo..hi),
    <lo:@L> <name:FunctionArgName> ":" <ty:TypeName> <hi:@R> => state.node(ast::CallArg::NamedType(ast::CallArgNamedType{name, ty}), lo..hi)
}

#[inline]
FunctionArgName: ast::SymbolPrimitive = {
    <name:"QuotedIdent"> => {
        ast::SymbolPrimitive {
            value: name.to_owned(),
            case: ast::CaseSensitivity::CaseSensitive
        }
    },
    <name:"UnquotedIdent"> => {
        ast::SymbolPrimitive {
            value: name.to_owned(),
            case: ast::CaseSensitivity::CaseInsensitive
        }
    }
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                               Path Navigation                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //
// Implementation of Path Navigation as specified by PartiQL Specification Section 4:
// https://partiql.org/assets/PartiQL-Specification.pdf
//
// Examples:
// a.b
// a.*
// a[*]
// a[*][*]
// a.b.c
// a[*].b[*].c
// "a".b
// "a"."b"
// { 'a': 1, 'b': 2 }.a
// [1, 2][1]
// foo(x, y).a
// (SELECT a from en).b
//
// See the path expression conformance tests under `partiql-tests` or parser unit-tests for more examples.
PathSteps: Vec<ast::PathStep> = {
    <path:PathSteps> <step:PathStep> => {
        let mut steps = path;
        steps.push(step);
        steps
    },
    <step:PathStep> => {
        vec![step]
    },
}
PathStep: ast::PathStep = {
    "." <v:PathExprVarRef> => {
        ast::PathStep::PathProject( ast::PathExpr{ index: Box::new(v) })
    },
    "[" "*" "]" => {
        ast::PathStep::PathForEach
    },
    "." "*" => {
        ast::PathStep::PathUnpivot
    },
    "[" <expr:ExprQuery> "]" => {
        ast::PathStep::PathIndex( ast::PathExpr{ index: Box::new(*expr) })
    },
}

PathExprVarRef: ast::Expr = {
    <lo:@L> <s:"String"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: s.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <VarRefExpr>,
}

VarRefExpr: ast::Expr = {
    <lo:@L> <ident:"UnquotedIdent"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <lo:@L> <ident:"QuotedIdent"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseSensitive },
            qualifier: ast::ScopeQualifier::Unqualified
      }, lo..hi)),
    <lo:@L> <ident:"UnquotedAtIdentifier"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseInsensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        }, lo..hi)),
    <lo:@L> <ident:"QuotedAtIdentifier"> <hi:@R> => ast::Expr::VarRef(state.node(ast::VarRef {
            name: ast::SymbolPrimitive { value: ident.to_owned(), case: ast::CaseSensitivity::CaseSensitive },
            qualifier: ast::ScopeQualifier::Unqualified
        },lo..hi)),
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                Literal Values                                  //
//                                                                                //
// ------------------------------------------------------------------------------ //

Literal: ast::Lit = {
    <LiteralAbsent>,
    <LiteralScalar>,
    <LiteralIon>,
    <TypedLiteral>,
}

#[inline]
LiteralAbsent: ast::Lit = {
    "NULL" => ast::Lit::Null,
    "MISSING" => ast::Lit::Missing,
}
#[inline]
LiteralScalar: ast::Lit = {
    <LiteralString>,
    <LiteralBool>,
    <LiteralNumber>
}
#[inline]
LiteralString: ast::Lit = {
    <s:"String"> => ast::Lit::CharStringLit(s.to_owned()),
}
#[inline]
LiteralBool: ast::Lit = {
    "TRUE" => ast::Lit::BoolLit(true),
    "FALSE" => ast::Lit::BoolLit(false),
}
#[inline]
LiteralNumber: ast::Lit = {
    // TODO check bounds before parsing?

    <lo:@L> <n:"Int"> <hi:@R> =>? {
        n.parse()
	        .map(ast::Lit::Int64Lit)
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
    <lo:@L> <r:"Real"> <hi:@R> =>? {
        rust_decimal::Decimal::from_str(r)
	        .map(ast::Lit::DecimalLit)
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
    <lo:@L> <r:"ExpReal"> <hi:@R> =>? {
        rust_decimal::Decimal::from_scientific(r)
	        .map(ast::Lit::DecimalLit)
		    .map_err(|_| lpop::ParseError::User{
	     	    error: ParseError::SyntaxError(
		            "invalid literal".to_string().to_located(BytePosition::from(lo)..BytePosition::from(hi)))
	         })
    },
}
#[inline]
LiteralIon: ast::Lit = {
    <ion:"Ion"> => ast::Lit::IonStringLit(ion.to_owned()),
}

#[inline]
TypeKeywordStr: &'static str = {
    "DATE" => "DATE",
    "TIME" => "TIME",
    "TIMESTAMP" => "TIMESTAMP",
    "WITH" => "WITH",
    "WITHOUT" => "WITHOUT",
    "ZONE" => "ZONE",
}

#[inline]
TypeKeyword: ast::SymbolPrimitive = {
    <s:TypeKeywordStr> => ast::SymbolPrimitive { value: s.to_string(), case: ast::CaseSensitivity::CaseInsensitive, },
}

#[inline]
TypeParam: ast::CustomTypeParam = {
    <lo:@L> <ty:TypeName> <hi:@R> => ast::CustomTypeParam::Type(ty),
    <lo:@L> <lit:LiteralScalar> <hi:@R> => ast::CustomTypeParam::Lit(lit),
}

TypeNamePart: ast::CustomTypePart = {
    <id:TypeKeyword> => ast::CustomTypePart::Name( id ),
    <id:TypeKeyword> "(" <args:CommaSepPlus<TypeParam>> ")" =>  ast::CustomTypePart::Parameterized( id, args ),
}

#[inline]
TypeName: ast::Type = {
    <parts:TypeNamePart+> => ast::Type::CustomType( ast::CustomType{ parts } ),
}

#[inline]
TypedLiteral: ast::Lit = {
    <ty:TypeName> <s:"String"> => ast::Lit::TypedLit(s.to_owned(), ty),
    // TODO we could support postgres-style literals with the following:
    //<s:"String"> "::" <ty:TypeName> => ast::Lit::TypedLit(s.to_owned(), ty),
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                  Utilities                                     //
//                                                                                //
// ------------------------------------------------------------------------------ //

// Comma as terminator (i.e. "<T>, <T>, <T>," where final comma is optional); may be empty
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaTermStar<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

// Comma as terminator (i.e. "<T>, <T>, <T>," where final comma is optional); at least 1 arg
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaTermPlus<T>: Vec<T> = {
    <mut v:(<T> ",")+> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

// Comma as separator (i.e. "<T>, <T>, <T>"); may be empty
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaSepStar<T>: Vec<T> = {
    <e:T?> <mut v:("," <T>)*> => match e {
         None => vec![],
         Some(e) => {
             v.insert(0,e);
             v
        }
    }
}

// Comma as separator (i.e. "<T>, <T>, <T>"); at least 1 arg
// This is a macro; see http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
CommaSepPlus<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
}

SymbolPrimitive: ast::SymbolPrimitive = {
    <ident:"UnquotedIdent"> => ast::SymbolPrimitive {
        value: ident.to_owned(),
        case: ast::CaseSensitivity::CaseInsensitive,
    },
    <ident:"QuotedIdent"> => ast::SymbolPrimitive {
        value: ident.to_owned(),
        case: ast::CaseSensitivity::CaseSensitive,
    },
}

AsIdent: ast::SymbolPrimitive = {
    "AS"? <SymbolPrimitive>
}

AtIdent: ast::SymbolPrimitive = {
    "AT" <SymbolPrimitive>
}

ByIdent: ast::SymbolPrimitive = {
    "BY" <SymbolPrimitive>
}

// ------------------------------------------------------------------------------ //
//                                                                                //
//                                    Lexer                                       //
//                                                                                //
// ------------------------------------------------------------------------------ //

// The lexer is external; See [`lexer.rs`] for its definition.
//
// See also http://lalrpop.github.io/lalrpop/lexer_tutorial/002_writing_custom_lexer.html
extern {
    type Location = ByteOffset;
    type Error = ParseError<'input, BytePosition>;

    enum lexer::Token<'input> {

        // Brackets
        "[" => lexer::Token::OpenSquare,
        "]" => lexer::Token::CloseSquare,
        "{" => lexer::Token::OpenCurly,
        "}" => lexer::Token::CloseCurly,
        "(" => lexer::Token::OpenParen,
        ")" => lexer::Token::CloseParen,
        "<<" => lexer::Token::OpenDblAngle,
        ">>" => lexer::Token::CloseDblAngle,

        // Punc
        "," => lexer::Token::Comma,
        ":" => lexer::Token::Colon,
        ";" => lexer::Token::Semicolon,
        "." => lexer::Token::Period,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        "*" => lexer::Token::Star,
        "/" => lexer::Token::Slash,
        "%" => lexer::Token::Percent,
        "^" => lexer::Token::Caret,
        "||" => lexer::Token::DblPipe,

        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        "!=" => lexer::Token::BangEqual,
        "<>" => lexer::Token::LessGreater,

        "<" => lexer::Token::LessThan,
        ">" => lexer::Token::GreaterThan,
        "<=" => lexer::Token::LessEqual,
        ">=" => lexer::Token::GreaterEqual,


        // Types
        "UnquotedIdent" => lexer::Token::UnquotedIdent(<&'input str>),
        "QuotedIdent" => lexer::Token::QuotedIdent(<&'input str>),
        "UnquotedAtIdentifier" => lexer::Token::UnquotedAtIdentifier(<&'input str>),
        "QuotedAtIdentifier" => lexer::Token::QuotedAtIdentifier(<&'input str>),
        "Int" => lexer::Token::Int(<&'input str>),
        "Real" => lexer::Token::Real(<&'input str>),
        "ExpReal" => lexer::Token::ExpReal(<&'input str>),
        "String" => lexer::Token::String(<&'input str>),
        "Ion" => lexer::Token::Ion(<&'input str>),

        // Keywords
        "ALL" => lexer::Token::All,
        "ASC" => lexer::Token::Asc,
        "AND" => lexer::Token::And,
        "AS" => lexer::Token::As,
        "AT" => lexer::Token::At,
        "BETWEEN" => lexer::Token::Between,
        "BY" => lexer::Token::By,
        "CASE" => lexer::Token::Case,
        "CROSS" => lexer::Token::Cross,
        "CYCLE" => lexer::Token::Cycle,
        "DATE" => lexer::Token::Date,
        "DESC" => lexer::Token::Desc,
        "DISTINCT" => lexer::Token::Distinct,
        "ELSE" => lexer::Token::Else,
        "END" => lexer::Token::End,
        "ESCAPE" => lexer::Token::Escape,
        "EXCEPT" => lexer::Token::Except,
        "FALSE" => lexer::Token::False,
        "FIRST" => lexer::Token::First,
        "FOR" => lexer::Token::For,
        "FULL" => lexer::Token::Full,
        "FROM" => lexer::Token::From,
        "GROUP" => lexer::Token::Group,
        "HAVING" => lexer::Token::Having,
        "IN" => lexer::Token::In,
        "INNER" => lexer::Token::Inner,
        "INTERSECT" => lexer::Token::Intersect,
        "IS" => lexer::Token::Is,
        "JOIN" => lexer::Token::Join,
        "LAST" => lexer::Token::Last,
        "LATERAL" => lexer::Token::Lateral,
        "LEFT" => lexer::Token::Left,
        "LIKE" => lexer::Token::Like,
        "LIMIT" => lexer::Token::Limit,
        "MISSING" => lexer::Token::Missing,
        "NATURAL" => lexer::Token::Natural,
        "NOT" => lexer::Token::Not,
        "NULL" => lexer::Token::Null,
        "NULLS" => lexer::Token::Nulls,
        "OFFSET" => lexer::Token::Offset,
        "ON" => lexer::Token::On,
        "OR" => lexer::Token::Or,
        "ORDER" => lexer::Token::Order,
        "OUTER" => lexer::Token::Outer,
        "PARTIAL" => lexer::Token::Partial,
        "PIVOT" => lexer::Token::Pivot,
        "PRESERVE" => lexer::Token::Preserve,
        "RIGHT" => lexer::Token::Right,
        "RECURSIVE" => lexer::Token::Recursive,
        "SELECT" => lexer::Token::Select,
        "SEARCH" => lexer::Token::Search,
        "TABLE" => lexer::Token::Table,
        "TIME" => lexer::Token::Time,
        "TIMESTAMP" => lexer::Token::Timestamp,
        "THEN" => lexer::Token::Then,
        "TRUE" => lexer::Token::True,
        "UNION" => lexer::Token::Union,
        "UNPIVOT" => lexer::Token::Unpivot,
        "USING" => lexer::Token::Using,
        "VALUE" => lexer::Token::Value,
        "VALUES" => lexer::Token::Values,
        "WHEN" => lexer::Token::When,
        "WHERE" => lexer::Token::Where,
        "WITH" => lexer::Token::With,
        "WITHOUT" => lexer::Token::Without,
        "ZONE" => lexer::Token::Zone,
    }
}
